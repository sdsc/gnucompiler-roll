#%Module1.0
module-whatis "GNU compilers"
module-whatis "Version: VERSION" 
module-whatis "Description: GNU compilers"

# If unloading, also unload any gnu-dependent modules
if {[module-info mode switch1] || [module-info mode switch3]} {
  # Get the command line that invoked this process; unload only if switching
  # from gnu to another (compiler) module, e.g., "module switch gnu intel"
  set commandWords [split [exec /bin/ps -p [pid] -o args=] " "]
  set switchFrom [lindex [split [module-info name] "/"] 0]
  set switchTo [lindex [split [lindex $commandWords end] "/"] 0]
  set unloadApps [expr ! [string equal $switchTo $switchFrom]]
} elseif {[module-info mode remove]} {
  set unloadApps 1
} else {
  set unloadApps 0
}
if {$unloadApps && [info exists env(LOADEDMODULES)]} {
# Check for intelmpi which doesn't have "gnu/" in it's path
  if { [ info exists env(I_MPI_CC) ] } {
     if { [ string equal $env(I_MPI_CC) gcc ] } {
       puts stderr "Unloading compiler-dependent module intelmpi"
       module unload intelmpi
     }
   }
  foreach app [lreverse [split $env(LOADEDMODULES) ":"]] {
    if {[string first "gnu/" $app] == 0} {
      continue
    }
    # Special case for gnutools, which installs into /opt/gnu
    if {[string first "gnutools/" $app] == 0} {
      continue
    }
    # Check whether this module references a compiler-specific subdir
    set moduleEffects [exec bash -c "module display $app" 2>@1]
    if {[string first "/gnu" $moduleEffects] >= 0} {
      puts stderr "Unloading compiler-dependent module $app"
      module unload [lindex [split $app "/"] 0]
    }
  }
}

# Add all gnu-dependent module dirs to the search path
foreach path [glob -nocomplain /opt/modulefiles/*/.gnu] {
  prepend-path MODULEPATH $path
}

# Compiler environment modifications
set gccHome /opt/gnu
setenv GCCHOME /opt/gnu
prepend-path PATH $gccHome/gcc/bin
prepend-path LD_LIBRARY_PATH $gccHome/gcc/lib64:$gccHome/gmp/lib:$gccHome/mpfr/lib:$gccHome/mpc/lib
